#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0,uppercase,10,10
10 GOTO 60
20 GOTO 1000

50 END


########################################
# THIS PROGRAM READS A COMPRESSED      #
# PRINTFOX GB FILE AND UNCOMPRESSES IT #
# FOR DISPLAY ON THE VDC-CHIP          #
#                                      #
# SOURCE FILE FOR MC128 MICROCOMPILER  #
#                                      #
# WRITTEN 2024 BY GOODWELL             #
#              FROM THE 8-BIT THEORY   #
########################################

# A=SOURCE ADDR
# B=SOURCE BANK
# C=DEST ADDR
# D=DEST BANK
# E=END ADDRESS OF INPUT FILE
# K=ORIGINAL BANK NUMBER
# L=LINEOFFSET
# M=MAGIC BYTE
# O=COLUMNCTR
# R=REPEATS
# V=VALUE
# Y=LOOP VARIABLE FOR UNPACK

# SOURCE BANK: 996
# SOURCE START ADDRESS (COMP GB):997/998
# SOURCE END ADDRESS: 999-1000

# DEST BANK: 1001
# DEST START ADDRESS (UNCOMP GB):1002/1003

60 K=PEEK(981)

# COMP.GB SOURCE
#  BANK
70 B=PEEK(996)

#  START ADDRESS
80 A=PEEK(997)
90 A=PEEK(998)*256+A

#   END ADDRESS
100 E=PEEK(999)
110 E=PEEK(1000)*256+E


# UNCOMP GB DESTINATION
#   BANK
150 D=PEEK(1001)

#   ADDRESS
160 C=PEEK(1002)
170 C=PEEK(1003)*256+C



# DEC("9B")=155
210 M=155
220 L=0
230 O=0

240 A=A+1

#250 DO

260  BANK B
270  V=PEEK(A)
280  A=A+1

290  IF V=M THEN GOSUB320:GOTO300
292  BANK D:GOSUB400

300 IF A<E THEN GOTO 260

314 BANK K

316 END


# UNPACK
320 R=PEEK(A)      :A=A+1
330 R=PEEK(A)*256+R:A=A+1
340 V=PEEK(A)      :A=A+1
350 BANK D
360 FOR Y = 1 TO R
370  GOSUB 400
380 NEXT
390 RETURN

#   COPY
400 POKE C+L,V
410 L=L+80

420 IF L<561 THEN RETURN

430 L=0:C=C+1:O=O+1
440 IF O>79 THEN O=0:C=C+560

450 RETURN


########################################
# THIS PROGRAM ORS TWO IMAGES IN RAM.  #
# IMAGE 1 IS READ AND ORED ONTO IMAGE2 #
# IMAGE SIZE IS ASSUMED AS 32000 BYTES #
#                                      #
# SOURCE FILE FOR MC128 MICROCOMPILER  #
#                                      #
# WRITTEN 2024 BY GOODWELL             #
#              FROM THE 8-BIT THEORY   #
########################################

# A=IMG1 ADDR
# B=IMG1 BANK
# C=IMG2 ADDR
# D=IMG2 BANK

# K=ORIGINAL BANK NUMBER
# V=VALUE
# O=OR-VALUE
# Y=COUNTER VARIABLE

# IMG1 BANK: 996
# IMG1 START ADDRESS (COMP GB):997/998

# IMG2 BANK: 1001
# IMG2 START ADDRESS (UNCOMP GB):1002/1003

1000 K=PEEK(981)

#  IMG1 BANK
1010 B=PEEK(996)

#  IMG1 START ADDRESS
1020 A=PEEK(997)
1030 A=PEEK(998)*256+A


#   IMG2 BANK
1040 D=PEEK(1001)

#   IMG2 ADDRESS
1050 C=PEEK(1002)
1060 C=PEEK(1003)*256+C


1070 Y=0

1080  BANK B
1090  V=PEEK(A+Y)

1100  BANK D
1110  O=PEEK(C+Y)
1120  POKE C+Y,O OR V

1130  Y=Y+1

1140  IF Y<32000 THEN 1080

1150 BANK K

1160 END