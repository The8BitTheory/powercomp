#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0,uppercase,10,10
#########################
# THIS ORS TWO GB FILES #
#########################

# SOURCE IMAGES RESIDE IN BANK 1
# DEST IMAGE IS WRITTEN TO BANK 0


4 S1=TI:FAST:GRAPHIC1:GRAPHIC0:GRAPHIC5:BANK15:POKE 57,0:POKE58,9:CLR:PRINT FRE(0) FRE(1)
5 DD=PEEK(186)


7 PRINT "LOADING GB.BIN...";:BLOAD"GB.BIN",B0,U(DD):PRINT "DONE"

# BASIC END (CONTAINS COMPRESSED GB FILE)
10 BE=PEEK(4624)+PEEK(4625)*256

# VARIABLE END (CONTAINS UNCOMPRESSED GB FILES)
12 VE=PEEK(57)+PEEK(58)*256

14 PRINT "BASIC END (COMPRESSED GB):"BE
16 PRINT "VARIABLE END (UNCOMP. GB):"VE

20 DEF FN HB(ZZ)=INT(ZZ/256)
21 DEF FN LB(ZZ)=ZZ-INT(ZZ/256)*256


100 FI$="RAHMEN.GB1"    :SB=0:SA=BE      :DB=1:DA=VE:GOSUB 1000
130 PRINT "DONE.":PRINT "FREE MEM:"FRE(0) FRE(1)


150 FI$="URKSCHRIFT2.GB":SB=0:SA=BE:GOSUB 2000
180 PRINT "DONE.":PRINT "FREE MEM:"FRE(0) FRE(1)


200 PRINT "UNPACKMERGING GB FILES"
205 DB=1:DA=VE

# IMG1 BANK: 996
# IMG1 START ADDRESS (UNCOMP GB):997/998

# IMG2 BANK: 1001
# IMG2 START ADDRESS (UNCOMP GB):1002/1003
210 POKE 996,SB
212 POKE 997,FNLB(SA):POKE 998,FNHB(SA)

220 POKE 999,FNLB(FE):POKE 1000,FNHB(FE)

225 POKE 1001,DB
226 POKE 1002,FNLB(DA):POKE1003,FNHB(DA)

249 PRINT "LOADING GBUNPACKOR.BIN...";:BLOAD"GBUNPACKOR.BIN",B0,U(DD):PRINT "DONE"

250 PRINT "MERGING... FROM B"SB","SA"-"FE" TO B"DB","DA;:S=TI:SYS DEC("1300"):S=TI-S
255 PRINT "DONE. TOOK "S" JIFFIES"

#257 PRINT "SAVE MERGED FILE... ";:BSAVE "@MERGED.GB",B0,P(BE) TO P(BE+32000),U(DD):PRINT"DONE"



# SOURCE BANK: 996
# SOURCE START ADDRESS (COMP GB):997/998

262 POKE 996,1
264 POKE 997,FNLB(VE):POKE 998,FNHB(VE)

# DEST BANK: 999
# DEST START ADDRESS (UNCOMP GB):1000/1001
266 POKE 999,0
268 POKE 1000,FNLB(BE):POKE 1001,FNHB(BE)

269 PRINT "LOADING GB.BIN...";:BLOAD"GB.BIN",B0,U(DD):PRINT "DONE"

270 PRINT "PACKING... FROM B1,"VE" TO B0"BE;:S=TI:SYS DEC("1309"):S=TI-S
275 PRINT "DONE. TOOK "S" JIFFIES"

276 EA=PEEK(1002)+PEEK(1003)*256
278 PRINT "PACKED GB FROM "BE" TO "EA

285 BSAVE"@ORPACKED.GB",B0,P(BE) TO P(EA),U(DD)


999 BANK15:GRAPHIC5:S1=TI-S1:PRINT "TOTAL:"S1:END


##############
# UNCOMPRESS #
##############
1000 GOSUB 2000



# SOURCE BANK
1030 POKE 996,SB

# SOURCE START ADDRESS
#1040 POKE 997,PEEK(4624):POKE 998,PEEK(4625)
1040 POKE 997,FNLB(SA):POKE 998,FNHB(SA)

# SOURCE END ADDRESS
1050 POKE 999,PEEK(174):POKE 1000,PEEK(175)

# DESTINATION BANK
1060 POKE 1001,DB

# DESTINATION ADDRESS
#1070 POKE 1002,FNLB(VE):POKE 1003,FNHB(VE)
1070 POKE 1002,FNLB(DA):POKE 1003,FNHB(DA)

1080 PRINT "UNCOMPRESSING TO: B"DB",DA"DA" - "(DA+32000)
1090 S=TI:SYS DEC("130C"):S=TI-S
1100 PRINT "DONE. TOOK "S" JIFFIES"

# STASH TO REU  :NR BYTES,RAM-ADDR,REU-ADDR,REU-BANK
# FETCH FROM REU:NR BYTES,RAM-ADDR,REU-ADDR,REU-BANK
#1105 IF DB=0 THEN 1199
#1110 BANK 1:STASH 32000,DA,0,0
#1120 BANK 0:FETCH 32000,BE,0,0
#1125 BANK 15

1199 RETURN


################
# LOAD GB FILE #
################
2000 PRINT "LOADING "FI$" TO B"SB",AD"SA;
2005 DOPEN #1,U(DD),(FI$),R
2010 GET#1,A$,B$
2020 DCLOSE #1

2030 IF A$<>"G" THEN PRINT FI$" IS NOT A GB FILE":END

# LOAD BINARY FILE. LEAVE TWO BYTES EMPTY AND MANUALLY POKE THE VALUES (BLOAD SKIPS THESE)
2040 BLOAD (FI$),B(SB),P(SA+2),U(DD)
2041 PRINT "DONE"


# FILE END (END OF COMPRESSED GB FILE
2042 FE=PEEK(174)+PEEK(175)*256
2044 PRINT "FILE END:"FE


2050 BANK SB
2052 POKE SA,ASC(A$):POKE SA+1,ASC(B$)
2054 BANK 15



2070 RETURN


