#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0,uppercase,10,10
10 GOTO 1000
30 GOTO 3000

# SOME COMMON CODE TO REDUCE SIZE OF BINARY

# DEC("9B")=155
100 M=155
120 K=PEEK(981)

#  BANK
130 B=PEEK(996)

#  START ADDRESS
132 A=PEEK(997)
134 A=PEEK(998)*256+A+1

140 RETURN




# END 
200 BANK K
210 END


########################################
# SOURCE: GBMERGE-MC.BAS               #
# THIS PROGRAM ORS TWO IMAGES IN RAM.  #
# IMAGE 1 IS READ AND ORED ONTO IMAGE2 #
# IMAGE SIZE IS ASSUMED AS 32000 BYTES #
#                                      #
# SOURCE FILE FOR MC128 MICROCOMPILER  #
#                                      #
# WRITTEN 2024 BY GOODWELL             #
#              FROM THE 8-BIT THEORY   #
########################################

# A=IMG1 ADDR
# B=IMG1 BANK
# C=IMG2 ADDR
# D=IMG2 BANK

# K=ORIGINAL BANK NUMBER
# V=VALUE
# O=OR-VALUE
# Y=COUNTER VARIABLE

# IMG1 BANK: 996
# IMG1 START ADDRESS (COMP GB):997/998

# IMG2 BANK: 1001
# IMG2 START ADDRESS (UNCOMP GB):1002/1003

#  IMG1 BANK
#1000 B=PEEK(996)

#  IMG1 START ADDRESS
#1020 A=PEEK(997)
#1030 A=PEEK(998)*256+A

1000 GOSUB 100

#   IMG2 BANK
1010 D=PEEK(1001)

#   IMG2 ADDRESS
1050 C=PEEK(1002)
1060 C=PEEK(1003)*256+C


1070 Y=0

1080  BANK B
1090  V=PEEK(A+Y)

1100  BANK D
1110  O=PEEK(C+Y)
1120  POKE C+Y,O OR V

1130  Y=Y+1

1140  IF Y<32000 THEN 1080

# END
1150 GOTO 200




########################################
# SOURCE: GBPACK-MC.BAS                #
# THIS PROGRAM READS UNCOMPRESSED      #
# PRINTFOX GB DATA AND COMPRESSES IT   #
#                                      #
# SOURCE FILE FOR MC128 MICROCOMPILER  #
#                                      #
# WRITTEN 2024 BY GOODWELL             #
#              FROM THE 8-BIT THEORY   #
########################################

# A=SOURCE ADDR
# B=SOURCE BANK
# C=DEST ADDR
# D=DEST BANK

# K=ORIGINAL BANK NUMBER
# M=MAGIC BYTE
# I=INPUT OFFSET
# O=OUTPUT OFFSET
# R=REPEATS
# L=REPEATS LOW-BYTE
# H=REPEATS HIGH-BYTE
# N=NEXT VALUE
# V=VALUE
# Y=LOOP VARIABLE

# SOURCE BANK: 996
# SOURCE START ADDRESS (COMP GB):997/998

# DEST BANK: 999
# DEST START ADDRESS (UNCOMP GB):1000/1001

# END ADDRESS OF PACKED FILE: 1002/1003


# UNCOMP.GB SOURCE
#  BANK
#2000 B=PEEK(996)

#  START ADDRESS
#2020 A=PEEK(997)
#2030 A=PEEK(998)*256+A


2000 GOSUB 100

# COMP.GB DESTINATION
#   BANK
2010 D=PEEK(999)

#   ADDRESS
2050 C=PEEK(1000)
2060 C=PEEK(1001)*256+C




2080 O=1
2090 I=0
2100 R=1

2110 BANK D:POKE C,71

#   SKIP THE HEADER-BYTE "G"($47, DEC 71)
#2120 A=A+1

2130 BANK B
2140 V=PEEK(A+I)

2150  BANK B
2160  I=I+1
2170  N=PEEK(A+I)

2180  IF N<>V THEN GOTO 2290

2190  R=R+1

2200  V=N

2210 IF I>31999 THEN GOTO 2230

2220 GOTO 2150

# POKE END-ADDRESS OF PACKED FILE TO MEMORY (EXCLUDING)
2230 R=C+O
2235 H=R/256
2260 L=H*256:L=R-L

2270 POKE 1002,L:POKE 1003,H

2280 GOTO 200


# VALUE IS DIFFERENT TO PREVIOUS, TIME TO WRITE DATA
2290 BANK D

2295 IF V=M THEN GOTO 2340
2300 IF R>4 THEN GOTO 2340
#2310 IF R>1 THEN GOTO 2500

# WRITE PLAIN VALUE
2320 FOR Y=1 TO R:POKE C+O,V:O=O+1:NEXT
#2320 POKE C+O,V:O=O+1
2325 R=1
2330 GOTO 2200


# WRITE PACKED VALUE
2340 POKE C+O,M:O=O+1

2360 H=R/256
2370 L=H*256:L=R-L

# REPEATS LOW-BYTE
2380 POKE C+O,L:O=O+1

# REPEATS HIGH-BYTE
2390 POKE C+O,H:O=O+1

2400 POKE C+O,V:O=O+1
2410 R=1

2420 GOTO 2200



########################################
# SOURCE: GBUNPACK-MC.BAS              #
#                                      #
# THIS PROGRAM READS A COMPRESSED      #
# PRINTFOX GB FILE AND UNCOMPRESSES IT #
#                                      #
# SOURCE FILE FOR MC128 MICROCOMPILER  #
#                                      #
# WRITTEN 2024 BY GOODWELL             #
#              FROM THE 8-BIT THEORY   #
########################################

# A=SOURCE ADDR
# B=SOURCE BANK
# C=DEST ADDR
# D=DEST BANK

# K=ORIGINAL BANK NUMBER
# L=LINEOFFSET
# M=MAGIC BYTE
# O=COLUMNCTR
# R=REPEATS
# V=VALUE
# Y=LOOP VARIABLE FOR UNPACK

# SOURCE BANK: 996
# SOURCE START ADDRESS (COMP GB):997/998
# SOURCE END ADDRESS: 999-1000

# DEST BANK: 1001
# DEST START ADDRESS (UNCOMP GB):1002/1003

# COMP.GB SOURCE
#  BANK
#3000 B=PEEK(996)

#  START ADDRESS
#3020 A=PEEK(997)
#3030 A=PEEK(998)*256+A

3000 GOSUB 100


#   END ADDRESS
3010 E=PEEK(999)
3050 E=PEEK(1000)*256+E


# UNCOMP GB DESTINATION
#   BANK
3060 D=PEEK(1001)

#   ADDRESS
3070 C=PEEK(1002)
3080 C=PEEK(1003)*256+C

3100 L=0
3110 O=0

#3120 A=A+1


3130  BANK B
3140  V=PEEK(A)
3150  A=A+1

3160  IF V=M THEN GOSUB3210:GOTO3180
3170  BANK D:GOSUB3290

3180 IF A<E THEN GOTO 3130

3190 GOTO 200


# UNPACK
3210 R=PEEK(A)      :A=A+1
3220 R=PEEK(A)*256+R:A=A+1
3230 V=PEEK(A)      :A=A+1
3240 BANK D
3250 FOR Y = 1 TO R
3260  GOSUB 3290
3270 NEXT
3280 RETURN

#   COPY
3290 POKE C+L,V
3300 L=L+1

3310 RETURN