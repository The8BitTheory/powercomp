#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0,uppercase,10,10
########################################
# THIS PROGRAM READS A COMPRESSED      #
# PRINTFOX GB FILE AND UNCOMPRESSES IT #
#                                      #
# SOURCE FILE FOR MC128 MICROCOMPILER  #
#                                      #
# WRITTEN 2024 BY GOODWELL             #
#              FROM THE 8-BIT THEORY   #
########################################

# A=SOURCE ADDR
# B=SOURCE BANK
# C=DEST ADDR
# D=DEST BANK

# K=ORIGINAL BANK NUMBER
# L=LINEOFFSET
# M=MAGIC BYTE
# O=COLUMNCTR
# R=REPEATS
# V=VALUE
# Y=LOOP VARIABLE FOR UNPACK

# SOURCE BANK: 996
# SOURCE START ADDRESS (COMP GB):997/998
# SOURCE END ADDRESS: 999-1000

# DEST BANK: 1001
# DEST START ADDRESS (UNCOMP GB):1002/1003

3000 K=PEEK(981)

# COMP.GB SOURCE
#  BANK
3010 B=PEEK(996)

#  START ADDRESS
3020 A=PEEK(997)
3030 A=PEEK(998)*256+A

#   END ADDRESS
3040 E=PEEK(999)
3050 E=PEEK(1000)*256+E


# UNCOMP GB DESTINATION
#   BANK
3060 D=PEEK(1001)

#   ADDRESS
3070 C=PEEK(1002)
3080 C=PEEK(1003)*256+C



# DEC("9B")=155
3090 M=155
3100 L=0
3110 O=0

3120 A=A+1

#250 DO

3130  BANK B
3140  V=PEEK(A)
3150  A=A+1

3160  IF V=M THEN GOSUB3210:GOTO3180
3170  BANK D:GOSUB3290

3180 IF A<E THEN GOTO 3130

3190 BANK K

3200 END


# UNPACK
3210 R=PEEK(A)      :A=A+1
3220 R=PEEK(A)*256+R:A=A+1
3230 V=PEEK(A)      :A=A+1
3240 BANK D
3250 FOR Y = 1 TO R
3260  GOSUB 3290
3270 NEXT
3280 RETURN

#   COPY
3290 POKE C+L,V
3300 L=L+1

3310 RETURN