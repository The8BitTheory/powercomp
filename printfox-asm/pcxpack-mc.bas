#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0,uppercase,10,10
10 GOTO 5000

# DEC("C0")=192
100 M=192
120 K=PEEK(981)

#  BANK
130 B=PEEK(996)

#  START ADDRESS
132 A=PEEK(997)
134 A=PEEK(998)*256+A+1

140 RETURN




# END
200 BANK K
210 END

######################################################
# SOURCE: PCXPACK-MC.BAS                             #
#
# ENCODES A RAW BIT STREAM TO PCX FILE               #
#                                                    #
# SO FAR, HARDCODED FOR GB-FORMAT DIMENSIONS         #
# + MONOCHROME                                       #
# + 640X400 RESOLUTION                               #
######################################################

5000 GOSUB 100

# COMP.GB DESTINATION
#   BANK
5010 D=PEEK(999)

#   ADDRESS
5020 C=PEEK(1000)
5030 C=PEEK(1001)*256+C




5040 O=1
5050 I=0
5060 R=1

#    WRITE PCX-HEADER
# 0XA - FILETYPE
5070 BANK D:POKE C,10

# VERSION 5
5080 V=5:GOSUB 5600

# RLE ENCODING
5090 V=1:GOSUB 5600

# NR OF BITS OF PLANE: 1
5100 V=1:GOSUB 5600

# MIN X-COORDINATE
5110 GOSUB 5570

# MIN Y-COORDINATE
5120 GOSUB 5570

# MAX X-COORDINATE
5130 V=127:GOSUB 5600
5140 V=2:GOSUB 5600

# MAX Y-COORDINATE
5150 V=143:GOSUB 5600
5160 V=1:GOSUB 5600

# RESOLUTION (2X2 BYTE FOR X AND Y)
5170 GOSUB 5570:GOSUB 5570

# COLOR PALETTE: 3X 00, THEN 3X FF, 42X 00
5180 V=255:GOSUB 5600
5190 V=255:GOSUB 5600
5200 V=255:GOSUB 5600
5210 GOSUB 5570
5220 V=0:GOSUB 5600


5230 FOR Y=0TO41:V=0:GOSUB 5600:NEXT

# FIRST RESERVED FIELD (0)
5240 V=0:GOSUB 5600

# NR OF COLOR PLANES (1)
5250 V=1:GOSUB 5600

# NR OF BYTES PER SCANLINE (80 DEC, 50 HEX) - 2 BYTES
5260 V=80:GOSUB 5600
5270 V=0:GOSUB 5600

# PALETTE MODE. 1=MONOCHROME OR COLOR
5280 V=1:GOSUB 5600
5285 V=0:GOSUB 5600

# SOURCE SYSTEM'S HOR AND VERT RESOLUTION. 2X2 BYTES OF ZERO
5290 GOSUB 5570:GOSUB 5570

# 54 BYTES OF ZEROES
5300 FOR Y=0 TO 53:V=0:GOSUB 5600:NEXT


# THIS READS RAW DATA, SO NO HEADER TO SKIP

5310 BANK B
5320 V=PEEK(A+I)

5330  BANK B
5340  I=I+1
5350  N=PEEK(A+I)

#     VALUE DIFFERENT TO NEXT VALUE -> WRITE DATA
5360  IF N<>V THEN GOTO 5470

#     MAX NR REPETITIONS PER RLE-PAIR REACHED -> WRITE DATA
5370  IF R>62 THEN GOTO 5470

5380  R=R+1

5390  V=N

5400 IF I>31999 THEN GOTO 5420

5410 GOTO 5330


# POKE END-ADDRESS OF PACKED FILE TO MEMORY (EXCLUDING)
5420 R=C+O
5430 H=R/256
5440 L=H*256:L=R-L

5450 POKE 1002,L:POKE 1003,H

5460 GOTO 200



# VALUE IS DIFFERENT TO PREVIOUS, TIME TO WRITE DATA
5470 BANK D

#    VALUE TOO LARGE, WE HAVE TO WRITE AN RLE-PAIR WITH THIS VALUE ONLY
5480 IF V>191 THEN GOTO 5530

#    MORE REPETITIONS
5490 IF R>2 THEN GOTO 5530

# WRITE PLAIN VALUE
5500 FOR Y=1 TO R:POKE C+O,V:O=O+1:NEXT

5510 R=1
5520 GOTO 5390



# WRITE RLE-PAIR
# BYTE 1: 2 MSB TO 11 -> C0 -> 192 (M)
#         6 BITS TO NR OF REPEATS (R)
# BYTE 2: THE ACTUAL BYTE VALUE
5530 POKE C+O,M + R:O=O+1

5540 POKE C+O,V:O=O+1
5550 R=1

5560 GOTO 5390

# CONVENIENCE
5570 POKE C+O,0:O=O+1
5580 POKE C+O,0:O=O+1
5590 RETURN


5600 POKE C+O,V:O=O+1:RETURN
