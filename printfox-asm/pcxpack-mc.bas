#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0,uppercase,10,10
10 GOTO 5000

# DEC("C0")=192
100 M=192
120 K=PEEK(981)

#  BANK
130 B=PEEK(996)

#  START ADDRESS
132 A=PEEK(997)
134 A=PEEK(998)*256+A+1

140 RETURN




# END
200 BANK K
210 END

######################################################
# SOURCE: PCXPACK-MC.BAS                             #
#
# ENCODES A RAW BIT STREAM TO PCX FILE               #
#                                                    #
# SO FAR, HARDCODED FOR GB-FORMAT DIMENSIONS         #
# + MONOCHROME                                       #
# + 640X400 RESOLUTION                               #
######################################################

5000 GOSUB 100

# COMP.GB DESTINATION
#   BANK
5010 D=PEEK(999)

#   ADDRESS
5020 C=PEEK(1000)
5030 C=PEEK(1001)*256+C




5040 O=1
5050 I=0
5060 R=1

#    WRITE PCX-HEADER
# 0XA - FILETYPE
5070 BANK D:POKE C,10

# VERSION 5
5080 POKE C+O,5:O=O+1

# RLE ENCODING
5090 POKE C+O,1:O=O+1

# NR OF BITS OF PLANE: 1
5100 POKE C+O,1:O=O+1

# MIN X-COORDINATE
5110 GOSUB 5570

# MIN Y-COORDINATE
5120 GOSUB 5570

# MAX X-COORDINATE
5130 POKE C+O,127:O=O+1
5140 POKE C+O,2:O=O+1

# MAX Y-COORDINATE
5150 POKE C+O,24:O=O+1
5160 POKE C+O,3:O=O+1

# RESOLUTION (2X2 BYTE FOR X AND Y)
5170 GOSUB 5570:GOSUB 5570

# COLOR PALETTE: 3X 00, THEN 3X FF, 42X 00
5180 GOSUB 5570
5190 POKE C+O,0:O=O+1
5200 POKE C+O,255:O=O+1
5210 POKE C+O,255:O=O+1
5220 POKE C+O,255:O=O+1

5230 FOR Y=0TO41:POKE C+O,0:O=O+1:NEXT

# FIRST RESERVED FIELD (0)
5240 POKE C+O,0:O=O+1

# NR OF COLOR PLANES (1)
5250 POKE C+O,1:O=O+1

# NR OF BYTES PER SCANLINE (80 DEC, 50 HEX) - 2 BYTES
5260 POKE C+O,80:O=O+1
5270 POKE C+O,0:O=O+1

# PALETTE MODE. 1=MONOCHROME OR COLOR
5280 POKE C+O,1:O=O+1

# SOURCE SYSTEM'S HOR AND VERT RESOLUTION. 2X2 BYTES OF ZERO
5290 GOSUB 5570:GOSUB 5570

# 54 BYTES OF ZEROES
5300 FOR Y=0 TO 53:POKE C+O,0:O=O+1:NEXT


# THIS READS RAW DATA, SO NO HEADER TO SKIP

5310 BANK B
5320 V=PEEK(A+I)

5330  BANK B
5340  I=I+1
5350  N=PEEK(A+I)

5360  IF N<>V THEN GOTO 5470
5370  IF R>63 THEN GOTO 5470

5380  R=R+1

5390  V=N

5400 IF I>31999 THEN GOTO 5420

5410 GOTO 5330


# POKE END-ADDRESS OF PACKED FILE TO MEMORY (EXCLUDING)
5420 R=C+O
5430 H=R/256
5440 L=H*256:L=R-L

5450 POKE 1002,L:POKE 1003,H

5460 GOTO 200



# VALUE IS DIFFERENT TO PREVIOUS, TIME TO WRITE DATA
5470 BANK D

5480 IF V>191 THEN GOTO 5530
5490 IF R>2 THEN GOTO 5530

# WRITE PLAIN VALUE
5500 FOR Y=1 TO R:POKE C+O,V:O=O+1:NEXT
#2320 POKE C+O,V:O=O+1
5510 R=1
5520 GOTO 5390



# WRITE RLE-PAIR
# BYTE 1: 2 MSB TO 11 -> C0 -> 192
#         6 BITS TO NR OF REPEATS
# BYTE 2: THE ACTUAL BYTE VALUE
5530 POKE C+O,M + R:O=O+1

5540 POKE C+O,V:O=O+1
5550 R=1

5560 GOTO 5390

# CONVENIENCE
5570 POKE C+O,0:O=O+1
5580 POKE C+O,0:O=O+1
5590 RETURN