#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC,uppercase,10,10
1 REM GB2PCX DRIVER PROGRAM
2 REM DOES THE FOLLOWING STEPS
3 REM 1) BLOAD GB FILE (SPECIAL TREATMENT FOR FIRST 2 BYTES)
4 REM 2) UNPACKS GB FILE BY USING GB2VDC.BIN (IE RE-ORGANIZING BYTES TO BE CONTINOUS)


10 GRAPHIC0:BANK15:POKE 57,0:POKE58,39:CLR

11 DEF FN HB(ZZ)=INT(ZZ/256)
12 DEF FN LB(ZZ)=ZZ-INT(ZZ/256)*256

14 DD=PEEK(186)

# BASIC END (CONTAINS COMPRESSED GB FILE)
16 BE=PEEK(4624)+PEEK(4625)*256

# VARIABLE END (CONTAINS UNCOMPRESSED GB FILES)
18 VE=PEEK(57)+PEEK(58)*256

20 PRINT "LOADING HIRES2VDC.BIN...";:BLOAD"HIRES2VDC.BIN",B0,U(DD):PRINT "DONE"
30 PRINT "LOADING VDCBASICAC6.BIN...";:BLOAD"VDCBASICAC6.BIN",B0,U(DD):SYSDEC("AC6")


#########################################
# VDC SETUP #
#############
35 RST 3

40 RGO 28,16  :REM 64K VRAM
50 RGO 25,128 :REM BITMAP MODE ON
60 RGA 25,191 :REM NO ATTRIBUTES

61 RGO 24,64  :REM REVERSE MODE

62 RGW 36,0   :REM MINIMAL SCREEN REFRESH CYCLES


70 RGW 0,63
73 RGW 1,40
74 RGW 2,56
75 RGO 25,16
76 RGW 22,8*16+9
77 RGW 27,40
78 POKE 238,39
79 RGW 2,56

##############################################


#   STEP 1
100 FI$="PM001-009":SB=1:SA=VE:GOSUB 2000

#   STEP 2
110 DB=0:DA=BE:GOSUB 1000

200 O=0
205 FOR X=0TO199
210  RTV BE+O,X*80,40
220  O=O+40
230 NEXT

999 END


###############################################
# UNCOMPRESS GB FILE TO CONTINUOUS BIT STREAM #
###############################################

# SOURCE BANK
1000 POKE 996,SB

# SOURCE START ADDRESS
#1040 POKE 997,PEEK(4624):POKE 998,PEEK(4625)
1040 POKE 997,FNLB(SA):POKE 998,FNHB(SA)

# SOURCE END ADDRESS
1050 POKE 999,PEEK(174):POKE 1000,PEEK(175)

# DESTINATION BANK
1060 POKE 1001,DB

# DESTINATION ADDRESS
#1070 POKE 1002,FNLB(VE):POKE 1003,FNHB(VE)
1070 POKE 1002,FNLB(DA):POKE 1003,FNHB(DA)

1080 PRINT "UNCOMPRESSING TO: B"DB",DA"DA" - "(DA+32000)
1090 S=TI:SYS DEC("1300"):S=TI-S
1092 PRINT "DONE. TOOK "S" JIFFIES"


1099 RETURN


#############################
# LOAD GB FILE TO BASIC-END #
#############################
2000 PRINT "LOADING "FI$" TO B"SB",AD"SA;
2005 DOPEN #1,U(DD),(FI$),R
2010 GET#1,A$,B$
2020 DCLOSE #1

2030 IF ASC(A$)+ASC(B$)*256<>8192 THEN PRINT FI$" IS MAYBE NOT A HIRES FILE"

# LOAD BINARY FILE. LEAVE TWO BYTES EMPTY AND MANUALLY POKE THE VALUES (BLOAD SKIPS THESE)
2040 BLOAD (FI$),B(SB),P(SA+2),U(DD)
2041 PRINT "DONE"


# FILE END (END OF COMPRESSED GB FILE
2042 FE=PEEK(174)+PEEK(175)*256
2044 PRINT "FILE END:"FE


2050 BANK SB
2052 POKE SA,ASC(A$):POKE SA+1,ASC(B$)
2054 BANK 15



2070 RETURN
